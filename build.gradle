plugins {
    id "de.marcphilipp.nexus-publish" version "0.4.0"
    id "io.codearte.nexus-staging" version "0.21.1"
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.11.0'
}

wrapper {
    gradleVersion = "${project.property('gradle.wrapper.version')}"
    distributionType = Wrapper.DistributionType.ALL
}

ext {
    user = 'jprante'
    name = 'gradle-plugin-jflex'
    pluginDescription = 'Gradle plugin for using JFlex'
    scmUrl = 'https://github.com/' + user + '/' + name
    scmConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
    scmDeveloperConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
}

apply plugin: 'java-library'
apply from: rootProject.file('gradle/ide/idea.gradle')
apply from: rootProject.file('gradle/compile/java.gradle')
apply from: rootProject.file('gradle/test/junit5.gradle')
apply from: rootProject.file('gradle/publishing/sonatype.gradle')

dependencies {
    api gradleApi()
    implementation "org.codehaus.groovy:groovy-all:${project.property('groovy.version')}"
    implementation "de.jflex:jflex:${project.property('jflex.version')}"
    testImplementation gradleTestKit()
}

validatePlugins {
    failOnWarning = false
}

gradlePlugin {
    plugins {
        jflexPlugin {
            id = 'org.xbib.gradle.plugin.jflex'
            implementationClass = 'org.xbib.gradle.plugin.JFlexPlugin'
        }
    }
}

if (project.hasProperty('gradle.publish.key')) {
    pluginBundle {
        website = scmUrl
        vcsUrl = scmUrl
        plugins {
            jflexPlugin {
                id = 'org.xbib.gradle.plugin.jflex'
                description = pluginDescription
                displayName = 'Gradle JFlex plugin'
                tags = ['jflex']
                version = project.version
            }
        }
    }
}
