
ext {
    user = 'jprante'
    name = 'gradle-plugin-jflex'
    pluginDescription = 'Gradle plugin for using JFlex'
    scmUrl = 'https://github.com/' + user + '/' + name
    scmConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
    scmDeveloperConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
}

buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'com.gradle.publish:plugin-publish-plugin:0.10.1'
  }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "com.gradle.plugin-publish"

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

configurations {
  wagon
}

dependencies {
  compile gradleApi()
  compile "de.jflex:jflex:1.7.0"
  wagon 'org.apache.maven.wagon:wagon-ssh:3.00'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all,-fallthrough"
    if (!options.compilerArgs.contains("-processor")) {
        options.compilerArgs << '-proc:none'
    }
}

task javadocJar(type: Jar, dependsOn: classes) {
  from javadoc
  classifier 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  from sourceSets.main.allSource
  classifier 'sources'
}

artifacts {
  archives javadocJar, sourcesJar
}


apply from: 'gradle/publish.gradle'

